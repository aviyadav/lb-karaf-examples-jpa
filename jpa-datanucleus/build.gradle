apply plugin: 'osgi'
apply plugin: 'maven'

version  = '1.0.0.SNAPSHOT'
group    = 'karaf-examples'

project.ext {
    bundleInportPackages = [
        'org.apache.felix.service.command',
        'org.apache.felix.gogo.commands',
        'org.apache.karaf.shell.console',
        'org.datanucleus.plugin',
        '*'
    ]
}

dependencies {
    compile 'javax.jdo:jdo-api:3.0.1'
    compile 'org.datanucleus:datanucleus-core:3.2.0-m4'
    compile 'org.datanucleus:datanucleus-cache:3.1.1'
    compile 'org.datanucleus:datanucleus-rdbms:3.2.0-m4'
    compile 'org.datanucleus:datanucleus-mongodb:3.2.0-m3'
    compile 'org.datanucleus:datanucleus-api-jpa:3.2.0-m4'
    compile 'org.datanucleus:datanucleus-api-jdo:3.2.0-m4'
}

task enhance << {
    println '>> enhance - begin'

    ant.taskdef( 
        name      : 'datanucleusenhancer',
        classpath : project.runtimeClasspath.asPath,
        classname : 'org.datanucleus.enhancer.EnhancerTask'
    )

    ant.datanucleusenhancer(
        classpath   : project.runtimeClasspath.asPath,
        verbose     : true,
        failonerror : true) {
        fileset( dir : "$buildDir/classes/main" ) {
            filename( name : "lb/examples/karaf/jpa/datanucleus/data/Item.class" )
        }
    }

    println '>> enhance - end'
}

jar {
    it.dependsOn enhance

    baseName = 'jpa-datanucleus'

    manifest {
        classesDir = new File("$buildDir/classes/main")
        classpath  = project.runtimeClasspath

        instruction 'Import-Package' , bundleInportPackages.join(',')
        instruction 'Export-Package' , '*'

        attributes(
            'Bundle-Description'     : 'Karaf Examples :: JPA :: DataNucleus',
            'Bundle-Name'            : 'Karaf Examples :: JPA :: DataNucleus',
            'Bundle-SymbolicName'    : project.name,
            'Bundle-Version'         : version,
            'JPA-PersistenceUnits'   : 'DATA_DATANUCLEUS',
            'Meta-Persistence'       : 'META-INF/persistence.xml',
            'DynamicImport-Package'  : '*'
        )
    }
}

