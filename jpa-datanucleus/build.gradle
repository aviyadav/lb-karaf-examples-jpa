project.ext {
    bundleInportPackages = [
        'org.apache.felix.service.command',
        'org.apache.karaf.shell.commands',
        'org.apache.karaf.shell.console',
        'org.datanucleus.plugin',
        '*'
    ]

    bundleExportPackages = [
        "*"
    ]
}

dependencies {
    compile project(':lb-karaf-examples-jpa-common')


    compile 'org.apache.geronimo.specs:geronimo-jpa_2.0_spec:1.1'
    compile 'org.apache.geronimo.specs:geronimo-jta_1.1_spec:1.1.1'
    compile 'javax.jdo:jdo-api:3.0.1'
    compile 'org.datanucleus:datanucleus-core:3.2.15'
    compile 'org.datanucleus:datanucleus-rdbms:3.2.12'
    compile 'org.datanucleus:datanucleus-api-jpa:3.2.6'
    compile 'org.datanucleus:datanucleus-api-jdo:3.2.8'
}

task enhance << {
    ant.taskdef( 
        name      : 'datanucleusenhancer',
        classpath : project.runtimeClasspath.asPath,
        classname : 'org.datanucleus.enhancer.EnhancerTask'
    )

    ant.datanucleusenhancer(
        classpath   : project.runtimeClasspath.asPath,
        verbose     : true,
        failonerror : true) {
        fileset( dir : "$buildDir/classes/main" ) {
            filename( name : "lb/examples/karaf/jpa/datanucleus/com.github.lburgazzoli.examples.karaf.jpa.commons.data/Item.class" )
        }
    }
}

jar {
    it.dependsOn enhance

    manifest {
        instruction 'Import-Package' , bundleInportPackages.join(',')
        instruction 'Export-Package' , bundleExportPackages.join(',')

        attributes['JPA-PersistenceUnits'  ] = 'DATA_DATANUCLEUS'
        attributes['Meta-Persistence'      ] = 'META-INF/persistence.xml'
        attributes['DynamicImport-Package' ] = '*'
    }
}
